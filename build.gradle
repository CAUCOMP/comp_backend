plugins {
	id 'java'
	id 'org.springframework.boot' version '4.0.2'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'checkstyle'
	id 'com.diffplug.spotless' version '6.25.0'
}

group = 'com.comp'
version = '0.0.1-SNAPSHOT'
description = 'University Club Community Website Project'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-data-redis'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.3.0'
	compileOnly 'org.projectlombok:lombok'
	runtimeOnly 'com.mysql:mysql-connector-j'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.boot:spring-boot-starter-security'
	testImplementation 'com.h2database:h2'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test') {
	useJUnitPlatform()
}

// Spotless configuration for code formatting
// Uses .editorconfig for consistent formatting rules
spotless {
	java {
		target 'src/*/java/**/*.java'
		removeUnusedImports()
		trimTrailingWhitespace()
		endWithNewline()
		indentWithSpaces(4)
		// Remove extra blank lines inside method bodies and before closing braces
		custom 'Remove blank line before closing brace', { content ->
			content.replaceAll(/\n\n(\s*\})/, '\n$1')
		}
	}
}

// Checkstyle configuration
checkstyle {
	toolVersion = '10.12.5'
	configFile = file("${projectDir}/config/checkstyle/checkstyle.xml")
	ignoreFailures = false
	maxWarnings = 0
}

tasks.withType(Checkstyle) {
	reports {
		xml.required = true
		html.required = true
	}
}

